plugins {
    id "java"
    id "java-library"
    id "checkstyle"
    id 'maven-publish'
}

group = 'com.fasterxml.jackson.datatype'
version = '0.1.1'
description = "Jackson module that adds support for serializing and deserializing MessagePack's Values to and from JSON."

repositories {
    mavenCentral()
}

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    api 'org.msgpack:msgpack-core:0.9.3'
    api 'com.fasterxml.jackson.core:jackson-annotations:2.15.0'
    api 'com.fasterxml.jackson.core:jackson-core:2.14.2'
    api 'com.fasterxml.jackson.core:jackson-databind:2.14.2'

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.hamcrest:hamcrest:2.2"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options {
        locale = 'en_US'
        encoding = 'UTF-8'
    }
}

sourcesJar {
    from rootProject.file("LICENSE")
}

javadocJar {
    from rootProject.file("LICENSE")
}

test {
    testLogging {
        outputs.upToDateWhen { false }
        showStandardStreams = true
    }
}

tasks.withType(Checkstyle) {
    reports {
        // Not to skip up-to-date checkstyles.
        outputs.upToDateWhen { false }
    }
}

checkstyle {
    toolVersion "9.3"
    configFile = file("${projectDir}/config/checkstyle/checkstyle.xml")
    ignoreFailures = true
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/calorie/jackson-datatype-msgpack")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
}
